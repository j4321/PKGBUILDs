# Maintainer: ValHue <vhuelamo at gmail dot com>
# https://github.com/ValHue/AUR-PKGBUILDs
#
# Contributor: Juliette Monsel <j_4321 at protonmail dot com>
# Contributor: Jon Nordby <jononor@gmail.com>
# Contributor: Alexandre Courbot <gnurou@gmail.com>

_pkgname="launchpadlib"
pkgname=("python-launchpadlib" "python2-launchpadlib")
pkgver="1.10.6"
pkgrel="1"
pkgdesc="A free Python library for scripting Launchpad through its web services interface."
arch=('any')
url="https://launchpad.net/launchpadlib"
license=('LGPL3')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://launchpad.net/$_pkgname/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://launchpad.net/$_pkgname/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.gz.asc")
sha256sums=('e4709c18214863d147c6e0415e695d5cfddefc0dbd6ea28eb03e6eada956ae8b'
            'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package_python2-launchpadlib() {
    depends=('python2-lazr-uri' 'python2-lazr-restfulclient'  'python2-wadllib'
             'python2-keyring' 'python2-httplib2' 'python2-simplejson')
    optdepends=('python2-testresources'  'python2-setuptools')
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python-launchpadlib() {
    depends=('python-lazr-uri' 'python-lazr-restfulclient' 'python-wadllib'
             'python-keyring' 'python-httplib2' 'python-simplejson')
    optdepends=('python-setuptools' 'python-testresources')
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
