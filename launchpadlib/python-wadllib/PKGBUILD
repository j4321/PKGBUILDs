# Maintainer: ValHue <vhuelamo at gmail dot com>
# https://github.com/ValHue/AUR-PKGBUILDs
#
# Contributor: Juliette Monsel <j_4321 at protonmail dot com>
# Contributor: György Balló <ballogy@freestart.hu>

_pkgname="wadllib"
pkgname=("python-wadllib" "python2-wadllib")
pkgver="1.3.2"
pkgrel="3"
pkgdesc="The Web Application Description Language is an XML vocabulary for describing the capabilities of HTTP resources. wadllib can be used in conjunction with an HTTP library to navigate and manipulate those resources"
arch=('any')
url="https://launchpad.net/wadllib"
license=('LGPL3')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://launchpad.net/$_pkgname/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://launchpad.net/$_pkgname/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.gz.asc")
sha256sums=('140e43fc16d4352a98a90a450c6326bee5e6de73ae373a569947f3b505405034'
            'SKIP')
validpgpkeys=('8417157EDBE73D9EAC1E539B126EB563A74B06BF')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package_python-wadllib() {
    depends=('python-lazr-uri' 'python-setuptools' 'python-simplejson')
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-wadllib() {
    depends=('python2-lazr-uri' 'python2-setuptools' 'python2-simplejson')
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
